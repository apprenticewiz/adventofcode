       IDENTIFICATION DIVISION.
       PROGRAM-ID. DAY03B.

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
       SELECT INPUT-FILE ASSIGN TO FILENAME
           ORGANIZATION IS LINE SEQUENTIAL.

       DATA DIVISION.
       FILE SECTION.
       FD INPUT-FILE.
       01 INPUT-REC  PIC X(128).
       
       WORKING-STORAGE SECTION.
       77 ARGC         PIC 9(4).
       77 ARG-COUNT    PIC Z(1).
       77 PROGNAME     PIC X(256).
       77 ARG          PIC X(256) VALUE SPACES.
       77 EOF-FLAG     PIC X(1) VALUE "N".
       77 BANK         PIC X(128).
       77 BANK-LEN     PIC 9(4).
       77 I            PIC 9(4).
       77 N            PIC 9(4).
       77 CURR-MAX     PIC 9(4).
       77 CURR-MAX-POS PIC 9(4).
       77 NUM-DIGITS   PIC 9(4).
       77 START-POS    PIC 9(4).
       77 END-POS      PIC 9(4).
       77 JOLTAGE      PIC 9(12).
       77 TOTAL        PIC 9(16) VALUE 0.
       77 RESULT       PIC Z(16).

       PROCEDURE DIVISION.

       ACCEPT ARGC FROM ARGUMENT-NUMBER

       DISPLAY 0 UPON ARGUMENT-NUMBER
       ACCEPT PROGNAME FROM ARGUMENT-VALUE

       EVALUATE TRUE
         WHEN ARGC IS LESS THAN 1
           DISPLAY "usage: " FUNCTION TRIM(PROGNAME) " <input file>"
           STOP RUN
       END-EVALUATE

       DISPLAY 1 UPON ARGUMENT-NUMBER
       ACCEPT ARG FROM ARGUMENT-VALUE
       MOVE FUNCTION TRIM(ARG) TO FILENAME

       OPEN INPUT INPUT-FILE
       PERFORM UNTIL EOF-FLAG = "Y"
         READ INPUT-FILE INTO INPUT-REC
           AT END
             MOVE "Y" TO EOF-FLAG
           NOT AT END
             MOVE INPUT-REC TO BANK
             PERFORM PROCESS-BANK
         END-READ
       END-PERFORM
       CLOSE INPUT-FILE
       MOVE TOTAL TO RESULT
       DISPLAY "result = " RESULT

       STOP RUN.
       
       PROCESS-BANK.
         MOVE FUNCTION LENGTH(FUNCTION TRIM(BANK)) TO BANK-LEN
         MOVE 1 TO START-POS
         MOVE 0 TO JOLTAGE
         PERFORM VARYING NUM-DIGITS FROM 12 BY -1 UNTIL NUM-DIGITS = 0
           COMPUTE END-POS = BANK-LEN - NUM-DIGITS + 1
           MOVE 0 TO CURR-MAX
           MOVE 0 TO CURR-MAX-POS
           PERFORM VARYING I FROM START-POS BY 1 UNTIL I > END-POS
             MOVE FUNCTION NUMVAL(BANK(I:1)) TO N
             IF N > CURR-MAX THEN
               MOVE N TO CURR-MAX
               MOVE I TO CURR-MAX-POS
             END-IF
           END-PERFORM
           COMPUTE JOLTAGE = JOLTAGE * 10 + CURR-MAX
           COMPUTE START-POS = CURR-MAX-POS + 1
         END-PERFORM
         COMPUTE TOTAL = TOTAL + JOLTAGE.
 